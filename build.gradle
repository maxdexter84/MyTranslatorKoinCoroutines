// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.4.21"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        def nav_version = "2.3.0"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath 'com.google.gms:google-services:4.3.4'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

/**
 * Так как у нас должно быть много модулей с одинаковыми параметрами,
 * такими как compileSdkVersion, minSdkVersion,
 * targetSdkVersion и Java compatibility,
 * имеет смысл автоматически прописывать эти параметры для всех модулей.
 * Сами данные для зависимостей мы уже берем из файла Dependencies.*/
subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android') ?: project.plugins.findPlugin('android-library')) {
            android {
                compileSdkVersion Config.compile_sdk

                defaultConfig {
                    minSdkVersion Config.min_sdk
                    targetSdkVersion Config.target_sdk
                }

                compileOptions {
                    sourceCompatibility Config.java_version
                    targetCompatibility Config.java_version
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}